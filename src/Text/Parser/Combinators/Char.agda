{-# OPTIONS --without-K --safe #-}

open import Text.Parser.Types.Core using (Parameters)

module Text.Parser.Combinators.Char {l} {P : Parameters l} where

open import Data.Unit.Base
open import Data.Bool.Base using (T; not)
open import Data.Char.Base using (Char)
open import Data.List.Base as List using ([]; _âˆ·_; null)
open import Data.List.NonEmpty as Listâº using (_âˆ·_)
open import Data.Maybe.Base using (nothing; just; maybe; fromMaybe)
open import Data.Nat.Base using (â„•)
import Data.Nat.Show as â„•
open import Data.Product using (_,_)
open import Data.String.Base as String using (String)
open import Data.Vec.Base as Vec using (toList)

open import Category.Monad using (RawMonadPlus)
open import Function.Base using (_âˆ˜â€²_; _$â€²_; _$_)

open import Relation.Nullary using (does)
open import Relation.Unary
open import Induction.Nat.Strong using (â–¡_)
open import Data.List.Sized.Interface using (Sized)
open import Data.Subset using (Subset; into)
open import Relation.Binary.PropositionalEquality.Decidable using (DecidableEquality; decide)

open import Level.Bounded
open import Text.Parser.Types P
open import Text.Parser.Combinators {P = P}
open import Text.Parser.Combinators.Numbers {P = P}
open Parameters P

module _ {{ğ•Š : Sized Tok Toks}}
         {{ğ•„ : RawMonadPlus M}}
         {{ğ”» : DecidableEquality (theSet Tok)}}
         {{â„‚ : Subset Char (theSet Tok)}}
         where

 private module â„‚ = Subset â„‚

 char : Char â†’ âˆ€[ Parser Tok ]
 char = exact âˆ˜â€² â„‚.into

 anyCharBut : Char â†’ âˆ€[ Parser Tok ]
 anyCharBut = anyTokenBut âˆ˜â€² â„‚.into

 noneOfChars : List.List Char â†’ âˆ€[ Parser Tok ]
 noneOfChars = noneOf âˆ˜â€² List.map â„‚.into

 anyOfChars : List.List Char â†’ âˆ€[ Parser Tok ]
 anyOfChars = anyOf âˆ˜â€² List.map â„‚.into

 space : âˆ€[ Parser Tok ]
 space = anyOf $â€² List.map â„‚.into $â€² ' ' âˆ· '\t' âˆ· '\n' âˆ· []

 spaces : âˆ€[ Parser (Listâº Tok) ]
 spaces = listâº space

 text : (t : String) {_ : T (not $â€² null $â€² String.toList t)} â†’
        âˆ€[ Parser (Listâº Tok) ]
 text t {pr} with String.toList t | pr
 ... | []     | ()
 ... | x âˆ· xs | _ = exacts $â€² Listâº.map â„‚.into (x âˆ· xs)

 module _ {A : Setâ‰¤ l} where

  parens : âˆ€[ â–¡ Parser A â‡’ Parser A ]
  parens = between (char '(') (box (char ')'))

  parens? : âˆ€[ Parser A â‡’ Parser A ]
  parens? = between? (char '(') (box (char ')'))

  withSpaces : âˆ€[ Parser A â‡’ Parser A ]
  withSpaces A = spaces ?&> A <&? box spaces

 lowerAlpha : âˆ€[ Parser Tok ]
 lowerAlpha = anyOf
            $â€² List.map â„‚.into
            $â€² String.toList "abcdefghijklmnopqrstuvwxyz"

 upperAlpha : âˆ€[ Parser Tok ]
 upperAlpha = anyOf
            $â€² List.map â„‚.into
            $â€² String.toList "ABCDEFGHIJKLMNOPQRSTUVWXYZ"

 alpha : âˆ€[ Parser Tok ]
 alpha = lowerAlpha <|> upperAlpha

 alphasâº : âˆ€[ Parser (Listâº Tok) ]
 alphasâº = listâº alpha

 num : âˆ€[ Parser [ â„• ] ]
 num = decimalDigit

 alphanum : âˆ€[ Parser (Tok âŠ [ â„• ]) ]
 alphanum = alpha <âŠ> num

module _ {{ğ•Š : Sized Tok Toks}}
         {{ğ•„ : RawMonadPlus M}}
         {{ğ”» : DecidableEquality (theSet Tok)}}
         {{â„‚  : Subset Char (theSet Tok)}}
         {{â„‚â» : Subset (theSet Tok)  Char}}
         where

 private
   module â„‚ = Subset â„‚
   module â„‚â» = Subset â„‚â»

 stringLiteral : âˆ€[ Parser [ String ] ]
 stringLiteral =
   convert <$> (char '"'
           &?> box escaped
           <&> box (listâº (unescaped <&?> box escaped)
           <?& char '"'))

   where

     toks : Setâ‰¤ _
     toks = Listâº Tok

     unescaped : âˆ€[ Parser toks ]
     unescaped = listâº (noneOfChars ('\\' âˆ· '"' âˆ· []))

     convertUnicode : Vec.Vec â„• 4 â†’ theSet toks
     convertUnicode ds = Listâº.map â„‚.into
                       $ '\\' âˆ· 'u' âˆ· List.map toChar (toList ds)

       where

         toChar : â„• â†’ Char -- only for hexadecimal digits
         toChar 0  = '0'
         toChar 1  = '1'
         toChar 2  = '2'
         toChar 3  = '3'
         toChar 4  = '4'
         toChar 5  = '5'
         toChar 6  = '6'
         toChar 7  = '7'
         toChar 8  = '8'
         toChar 9  = '9'
         toChar 10 = 'a'
         toChar 11 = 'b'
         toChar 12 = 'c'
         toChar 13 = 'd'
         toChar 14 = 'e'
         toChar _  = 'f'

     escaped : âˆ€[ Parser toks ]
     escaped =
       let unicode : âˆ€[ Parser (Maybe toks) ]
           unicode = just âˆ˜â€² convertUnicode <$> replicate 4 hexadecimalDigit

           chunks : âˆ€[ Parser (Listâº toks) ]
           chunks = listâº ((Î» (a , mb) â†’ fromMaybe (a âˆ· []) mb)
             <$> (char '\\' -- escaping
             &> box ((_, nothing) <$> anyOfChars ('"' âˆ· '\\' âˆ· 'r' âˆ· 'n' âˆ· 't' âˆ· [])) -- special characters
               <|> char 'u' <&> box unicode))
       in Listâº.concat <$> chunks

     convert : theSet (Maybe toks Ã— Maybe (Listâº (toks Ã— Maybe toks))) â†’ String
     convert (mt , mts) = let open List in
       String.fromList $
         fromMToks mt ++
          maybe (concatMap (Î» (ts , mts) â†’ fromToks ts ++ fromMToks mts) âˆ˜â€² Listâº.toList) [] mts

       where

         fromToks : theSet toks â†’ List.List Char
         fromToks = Listâº.toList âˆ˜â€² Listâº.map â„‚â».into

         fromMToks : theSet (Maybe toks) â†’ List.List Char
         fromMToks = maybe fromToks []
